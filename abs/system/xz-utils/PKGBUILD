# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=xz-utils
pkgver=4.999.8beta
pkgrel=2
pkgdesc="Newer version of lzma-utils"
arch=('i686' 'x86_64')
url="http://tukaani.org/xz/"
license=('GPL')
depends=('bash')
# eventually this will conflict, provide and replace lzma-utils,
# but since the formats are incompatible, we provide it as an extra
makedepends=('gcc' 'util-linux-ng')
options=('!libtool' 'zipman')
source=(${url}xz-$pkgver.tar.gz)
md5sums=('f00967331a487e88d51207fe17c56f52')

build() {
  cd "$srcdir/xz-$pkgver"

  # This is in order to prevent conflicts with lzma-utils:
  sed -i 's/lz/xz/g' src/scripts/Makefile.am || return 1
  sed -i 's/lzma/xz/g' src/scripts/lz* || return 1
  rename lz xz src/scripts/lz* || return 1

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir" install
  
  # This is in order to prevent conflicts with lzma-utils:
  rm -f $pkgdir/usr/bin/lz*
  rm -f $pkgdir/usr/bin/unlzma
}

