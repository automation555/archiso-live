# Maintainer:  TDY <tdy@gmx.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=uget
pkgver=1.5.0.1
pkgrel=1
pkgdesc="A download manager featuring download classification and HTML import"
arch=('i686' 'x86_64')
url="http://urlget.sourceforge.net/"
license=('LGPL')
depends=('curl>=7.17' 'gtk2>=2.14' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig>=0.9.0')
install=uget.install
source=(http://downloads.sourceforge.net/urlget/$pkgname-$pkgver.tar.gz)
md5sums=('707ebf0d45d1b670194b262bbad6704f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-notify --disable-gstreamer
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
