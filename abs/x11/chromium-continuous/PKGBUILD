# Contributor: RandyPenguin <randypenguin {at} gmail.com>
# Credits: alexwizard
# Credits: thotypous
# Credits: xduugu

pkgname=chromium-continuous
_realname=chromium-browser
_latest=$(wget -qO - http://build.chromium.org/buildbot/continuous/linux/LATEST/REVISION)
pkgver=$_latest
pkgrel=latest
pkgdesc="Up to date testing build of the Chromium Web Browser (basis for Google Chrome)"
arch=('i686' 'x86_64')
url="http://build.chromium.org/"
license=('BSD')
[ "${CARCH}" == "i686" ] && depends=('alsa-lib' 'gconf' 'nss' 'xdg-utils')
[ "${CARCH}" == "x86_64" ] && depends=('alsa-lib' 'gconf' 'nss' 'xdg-utils' \
    'lib32-alsa-lib' 'lib32-cairo' 'lib32-dbus-glib' 'lib32-freetype2' \
    'lib32-gconf' 'lib32-gtk2' 'lib32-nspr' 'lib32-nss' 'lib32-openssl' 'lib32-libxdamage')
provides=(${_realname})
conflicts=('chromium-browser' 'chromium-snapshot' 'chromium-fresh' 'chromium-lkgr')
source=("${_realname}.png" "${_realname}.desktop" "BSD-LICENSE" "${_realname}-64" "${_realname}-32")
md5sums=('38877d28d1473fd9027ce579f70c31ad'
         '327aaba0e8d7d5ec34e4263c623db850'
         '9e854fbdcf3d805b461da0ef9ebee645'
         '150802a408d622ed4c029505260b2691'
         'f4a11402f5f8ba2a2b55361cac1120e7')

build() {
  cd ${srcdir}

  wget -O "$SRCDEST/chrome-linux.zip" "http://build.chromium.org/buildbot/snapshots/chromium-rel-linux/${pkgver}/chrome-linux.zip" \
  && bsdtar xf "$SRCDEST/chrome-linux.zip" || return 1

  mkdir -p ${pkgdir}/opt/${_realname}
  cp -r ./chrome-linux/* ${pkgdir}/opt/${_realname}

  # patch executable
  cd ${pkgdir}/opt/${_realname}
  sed -i 's,libnss3.so.1d,libnss3.so\x00\x00\x00,g;
           s,libnssutil3.so.1d,libnssutil3.so\x00\x00\x00,g;
           s,libsmime3.so.1d,libsmime3.so\x00\x00\x00,g;
           s,libssl3.so.1d,libssl3.so\x00\x00\x00,g;
           s,libplds4.so.0d,libplds4.so\x00\x00\x00,g;
           s,libplc4.so.0d,libplc4.so\x00\x00\x00,g;
           s,libnspr4.so.0d,libnspr4.so\x00\x00\x00,g;' chrome || return 1

  # install icon, desktop and license files
  install -Dm644 ${srcdir}/${_realname}.png ${pkgdir}/usr/share/pixmaps/${_realname}.png
  install -Dm644 ${srcdir}/${_realname}.desktop ${pkgdir}/usr/share/applications/${_realname}.desktop
  install -Dm644 ${srcdir}/BSD-LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/BSD-LICENSE

  # install wrapper script for i686 or x86_64
  if [ "${CARCH}" == "x86_64" ]; then
    install -Dm755 ${srcdir}/${_realname}-64 ${pkgdir}/usr/bin/${_realname}
  else
    install -Dm755 ${srcdir}/${_realname}-32 ${pkgdir}/usr/bin/${_realname}
  fi
}
