# Maintainer: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Contributor: niQo
pkgname=virtualbox-ose
pkgver=2.1.2
pkgrel=1
pkgdesc="Powerful x86 virtualization for enterprise as well as home use (Open Source Edition)"
url="http://www.virtualbox.org"
license=('GPL' 'custom')
arch=('i686')
provides=('virtualbox')
conflicts=('virtualbox')
depends=('virtualbox-modules' 'libxcursor' 'libidl2' 'libxslt' 'gcc-libs'
	'xalan-c')
makedepends=('gcc34' 'libstdc++5' 'bin86' 'dev86' 'iasl' 'libxslt' 'libxml2'
	'libxcursor' 'qt3' 'qt' 'libidl2' 'sdl' 'sdl_ttf' 'alsa-lib' 'pulseaudio'
	'hal')
install=virtualbox.install
_realver=2.1.2_OSE
source=(http://download.virtualbox.org/virtualbox/$pkgver/VirtualBox-$pkgver-OSE.tar.bz2
        '60-virtualbox.rules')
build() {
  cd "$srcdir/VirtualBox-$_realver"
  . /etc/profile.d/qt3.sh
  export PATH=$QTDIR/bin:$PATH
  export QMAKESPEC=$QTDIR/mkspecs/linux-g++

  sed -i 's/python2.5/python2.6/g' configure
  ./configure --with-qt-dir=/opt/qt \
    --with-gcc=/usr/bin/gcc-3.4 \
    --with-g++=/usr/bin/g++-3.4 \
    --disable-hardening
  source ./env.sh
  kmk all || return 1

  cd out/linux.$BUILD_PLATFORM_ARCH/release/bin
  rm -rf sdk src tst* testcase SUPInstall SUPUninstall
  mkdir -p $pkgdir/usr/{bin,lib/virtualbox,share/virtualbox}

  install -m644 *.so "$pkgdir/usr/lib/virtualbox/"
  install -m644 *.gc *.r0 ../obj/tstVMM/tstVMM \
    "$pkgdir/usr/lib/virtualbox/"

  # if you move the binaries in /usr/share the sky will fall on your head
  for each in VBoxBFE VBoxHeadless VBoxManage VBoxSDL VBoxSVC VBoxTunctl VBoxXPCOMIPCD VirtualBox xpidl; do
    install -m755 $each "$pkgdir/usr/lib/virtualbox/$each"
    ln -fs /usr/lib/virtualbox/$each "$pkgdir/usr/bin/$each"
  done

  install -D -m644 "$srcdir/VirtualBox-$_realver/src/VBox/Installer/linux/VBox.sh" \
                   "$pkgdir/usr/bin/Vbox.sh"

  #install -D -m755 "$srcdir/VirtualBox-$_realver/src/VBox/Installer/linux/VBoxAddIF.sh" \
  #                 "$pkgdir/usr/bin/VBoxAddIF"
  
  cp -R components "$pkgdir/usr/lib/virtualbox/"

  cp -R nls/ "$pkgdir/usr/share/virtualbox/"
  cd "$pkgdir/usr/lib/virtualbox/"; ln -s ../VBox* .

  install -D -m644 "$srcdir/VirtualBox-$_realver/src/VBox/Installer/linux/VirtualBox.desktop" \
    "$pkgdir/usr/share/applications/virtualbox.desktop"
  install -D -m644 "$srcdir/VirtualBox-$_realver/src/VBox/Frontends/VirtualBox/images/OSE/VirtualBox_64px.png" \
                   "$pkgdir/usr/share/pixmaps/VBox.png"

  install -D -m644 "$srcdir/VirtualBox-$_realver/COPYING" \
                   "$pkgdir/usr/share/licenses/virtualbox-ose/LICENSE"

  install -D -m644 "$srcdir/60-virtualbox.rules" \
                   "$pkgdir/etc/udev/rules.d/60-virtualbox.rules"
}
md5sums=('ce1360945d65a79dbea1bec997c25c00'
         '519d32d8c2408e0ed9d643f412117644')
