# Maintainer: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Contributor: niQo
# Contributor: Ingmar Steen 
 
pkgname=virtualbox-modules
_kernver=2.6.29-ARCH
pkgver=2.2.2
pkgrel=1
pkgdesc="Kernel modules for VirtualBox (vboxdrv)"
arch=('i686')
url="http://www.virtualbox.org"
license=('GPL')
depends=('kernel26>=2.6.29' 'kernel26<2.6.30')
makedepends=('bin86' 'dev86' 'iasl' 'libstdc++5' 'qt3' 'sdl_ttf' 'libxt'
             'libgl' 'alsa-lib' 'pulseaudio' 'gcc34' 'libxcursor' 'libidl2' 'libxslt'
             'xalan-c' 'pth' 'libxml2' 'libidl2' 'libpng')
install=virtualbox-modules.install
source=(http://download.virtualbox.org/virtualbox/$pkgver/VirtualBox-$pkgver-OSE.tar.bz2)
#source=($pkgname.tar.gz)
build() {
  cd "$srcdir/VirtualBox-2.2.2_OSE"
  sed -i 's/python2.5/python2.6/g' configure
  ./configure \
    --with-gcc=/usr/bin/gcc-3.4 \
    --with-g++=/usr/bin/g++-3.4 \
    --disable-hardening \
    --disable-pulse

  sed -i 's/-DVBOX_WITH_HARDENING//' src/VBox/HostDrivers/Support/linux/Makefile
  
  source ./env.sh
  kmk all || return 1
  
  cd out/linux.x86/release/bin/src
  make || return 1
  install -D -m644 vboxdrv.ko "$pkgdir/lib/modules/${_kernver}/misc/vboxdrv.ko"
  install -D -m644 vboxnetflt.ko "$pkgdir/lib/modules/${_kernver}/misc/vboxnetflt.ko"
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/virtualbox-modules.install"
}
md5sums=('82a76247b63e1813695175e7ddf01975')

